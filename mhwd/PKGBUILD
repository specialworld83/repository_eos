# Fork Manjaro Hardware Detection
# maintainer: Philip Müller <philm[at]manjaro[dog]org>
# maintainer: Roland Singer <roland[at]manjaro[dog]org>
# Maintainer: Calogero Scarnà <info@codelinsoft.it>

pkgbase=mhwd
pkgname=('mhwd' 'mhwd-db')
pkgver=0.6.4
pkgrel=10
pkgdesc="EndeavourOS Hardware Detection"
arch=('i686' 'x86_64')
url="http://git.manjaro.org"
license=('GPL')
depends=('gcc-libs' 'hwinfo')
makedepends=('git' 'cmake')
source=(git+https://gitlab.manjaro.org/applications/mhwd.git#commit=eed9e12887bcd54749fbacdb0d2e515b163f1e32
        git+https://gitlab.manjaro.org/applications/mhwd-db.git#commit=ca9666f373a90c3de4194bda7fa131d9cc338322)
sha256sums=(SKIP SKIP)

build() {
  cd $pkgname

  if [ -e "$srcdir"/../mhwd-$pkgver-$pkgrel.patch ]; then
     patch -Np1 -i "$srcdir"/../mhwd-$pkgver-$pkgrel.patch
  fi

  cmake ./
  make all
}

package_mhwd() {
  pkgdesc="EndeavourOS Hardware Detection library and application"
  depends=('hwinfo' 'mesa' 'mhwd-db' 'v86d' 'pacman')
  if [ "${CARCH}" = "x86_64" ]; then
     optdepends=('lib32-mesa: for 32bit libgl support')
  fi
  install=mhwd.install

  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir"/var/lib/mhwd/{db,local}/{pci,usb}
}

package_mhwd-db() {
  pkgdesc="EndeavourOS Hardware Detection Database"
  arch=('i686' 'x86_64')
  install=mhwd-db.install

  cd $pkgname

  if [ -e "$srcdir"/../mhwd-db-$pkgver-$pkgrel.patch ]; then
     patch -Np1 -i "$srcdir"/../mhwd-db-$pkgver-$pkgrel.patch
  fi

  mkdir -p "$pkgdir"/var/lib/mhwd/db
  mkdir -p "$pkgdir"/etc/X11/mhwd.d

  cp -r pci "$pkgdir"/var/lib/mhwd/db/
  # cp -r usb "$pkgdir"/var/lib/mhwd/db/

  # remove nvidia on i686
  if [ "${CARCH}" = "i686" ]; then
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/nvidia-418xx/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/nvidia-430xx/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/nvidia-435xx/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/nvidia-440xx/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/hybrid-intel-nvidia-418xx-bumblebee/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/hybrid-intel-nvidia-430xx-bumblebee/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/hybrid-intel-nvidia-435xx-prime/
     rm -r "$pkgdir"/var/lib/mhwd/db/pci/graphic_drivers/hybrid-intel-nvidia-440xx-prime/
  fi
}
